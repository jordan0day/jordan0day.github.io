---
layout: post
title: Streams, part three - Introducing the scan function
category: elixir
tags: enum stream
excerpt: In the last entry we used Elixir streams to calculate pi using infinite series that converged to pi's true value. In this entry, we'll figure out how to run the series for only exactly as many iterations as necessary to reach a certain level of precision.
---

In the previous entry, we used both the Gregory-Leibniz series and the Nilakantha series to calculate an approximation of pi. While the Nilakantha series wasn't exactly hyper-efficient (it took somewhere between 10 and 100 iterations to identify the first six digits of pi), it was still much quicker than the Leibniz series, which took somewhere between 100,000 and 1,000,000 iterations. It would be nice if we could specify a certain number of digits and just let the calculation run until we get to the desired level of precision[^1]. Now, you don't think I'd write all that without actually having a way of doing it, do you?

I don't know if anyone is keeping score, but `map` and `reduce` are probably the two most-used functions in the `Enum` module. A quick glance at the `Stream` module will show a similar `map` function, but where's `reduce`? In functional programming, there is a lazy operation similar to `reduce`, known as `scan`.[^2] Scan works sort of like reduce, but instead of waiting until the end of the enumerable to produce a result, `scan` returns the result on each iteration, and passes that result in to the next iteration as the accumulator. Here's a quick example of the difference:

    numbers = [1,2,3]
    Enum.reduce(numbers, fn (x, acc) -> x + acc end)
    6

    scan_stream = Stream.scan(numbers, fn (x, acc) -> x + acc end)
    #Stream<[enum: [1, 2, 3], funs: [#Function<56.29647706/1 in Stream.scan/2>]]>

    Enum.take(scan_stream, 3)
    [1, 3, 6]

When we use `Enum.reduce`, the only thing we get back is the final answer. When we use `Stream.scan`, we instead get the answer *for each iteration*. So, we can use `scan` on our pi-calculating functions to produce a list of the outputs of each iteration, from which we can find exactly which iteration produced a given level of precision.

To recap the previous post, here's the functions for the Leibniz series and the Nilakantha series, as well as how we used them in conjunction with `Enum.reduce`:

    odds = Stream.iterate(1, fn x -> x + 2 end)

    leibniz = fn elem, acc ->
      if rem(1 + elem, 4) == 0 do
        acc - (4 / elem)
      else
        acc + (4 / elem)
      end
    end

    odds |> Stream.take(1_000_000) |> Enum.reduce(0, leibniz)
    3.1415916535897743

    # Start our stream at 3 for the Nilakantha series...
    odds3 = Stream.iterate(3, fn x -> x + 2 end)

    nilakantha = fn elem, acc ->
      denominator = (elem - 1) * elem * (elem + 1)
      if rem(1 + elem, 4) == 0 do
        acc + (4 / denominator)
      else
        acc - (4 / denominator)
      end
    end

    odds3 |> Stream.take(100) |> Enum.reduce(3, nilakantha)
    3.1415924109719824

First, let's use `scan` to figure out at exactly which iteration the Leibniz series first converges to the true first six digits of pi (3.14159):

    leibniz_scan = odds |> Stream.scan(0, leibniz)
    #Stream<[enum: #Function<47.29647706/2 in Stream.unfold/2>,
    funs: [#Function<35.29647706/1 in Stream.scan/3>]]>

Like the other `Stream` module functions, `scan` returns an `Enumerable`, so we need to process it with something in the `Enum` module to actually get what we're looking for. In this case, we can use `Enum.take_while` to take results from `scan` only until we've found one that has converged to 3.14159xxxxx. Elixir's `Float.floor` function can be used to round down, with an optional argument allowing us to specify how many decimal digits should remain, so we'll use that for our comparison function:[^3]

    check_approx = fn x -> Float.floor(x, 5) != 3.14159 end

    result = Enum.take_while(leibniz_scan, check_approx)
    [4.0, 2.666666666666667, 3.466666666666667, 2.8952380952380956,
     3.3396825396825403, 2.9760461760461765, 3.2837384837384844, 3.017071817071818,
     3.2523659347188767, 3.0418396189294032, ...]

    length(result)
    136120

So it looks like the Leibniz series converges on 3.14159xxxxx on the 136,121st iteration (remember, take_while will only return the elements for which `check_approx` returns true, so on iteration 136,121, it returned false). We can double-check this by plugging 136121 back into our `Enum.reduce` call from before:[^4]

    odds |> Stream.take(136_121) |> Enum.reduce(0, leibniz)
    3.141599999994786

Now, let's do the same thing for the Nilakantha series:

    nilakantha_scan = odds3 |> Stream.scan(3, nilakantha)
    #Stream<[enum: #Function<47.29647706/2 in Stream.unfold/2>,
    funs: [#Function<35.29647706/1 in Stream.scan/3>]]>

    result = Enum.take_while(nilakantha_scan, check_approx)
    [3.1666666666666665, 3.1333333333333333, 3.145238095238095, 3.1396825396825396,
     3.1427128427128426, 3.1408813408813407, 3.142071817071817, 3.1412548236077646,
     3.141839618929402, 3.1414067184965018, ...]

     length(result)
     32

     odds3 |> Stream.take(33) |> Enum.reduce(3, nilakantha)
     3.1415990074057167

So, the 33rd iteration is where we first see 3.14159 in the Nilakantha series. About 4,000 times sooner than the Leibniz series. In the next post, we'll get away from calculating the digits of pi and take a look at some of the other functions in `Stream`.

[^1]: There's probably a better way to put this than the term "precision", since this is all about approxmations. In fact, because the Leibniz series and Nilakantha series are infinite series that *converge* on the true value of pi, it's actually possible for it to take several iterations before the correct digit "settles into place", so to speak. For example, the first 7 iterations of the Leibniz series look like: 4.0, 2.6..., 3.4..., 2.8..., 3.3..., 2.9..., 3.2.... You can see that the first digit bounces between 2 and 3 on iterations 2 through 7. It's not until iteration 7 that 3 has finally "settled" into place, and won't change again in the series. For the sake of brevity, we'll look for *the first time* the sequence 3.14159 appears in either series, and not for the point where it "settles".

[^2]: https://en.wikipedia.org/wiki/Prefix_sum#Scan_higher_order_function

[^3]: I know that floating-point math is fraught with peril, so it's entirely possible that this method is only working on my computer and is utterly failing for you. If so... sorry? Floating points gonna float, you know?

[^4]: You could also used `Enum.at` here, but since `Enum.at` is zero-based, and I just got done making a whole big deal about iteration 136120 versus 136121, I didn't want to further confuse the plot.