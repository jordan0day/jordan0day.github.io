---
layout: post
title: Streams, part two - Doing something a little bit useful
category: elixir
tags: enum stream
excerpt: Building on the previous entry, we actually do something halfway interesting with our stream of the odd natural numbers.
---

In the previous entry, I wrote a little about what Elixir streams are, and how they can differ from the enumerables you typically work with using the Enum module. Then I showed how to use the Stream module's `iterate` function to create a stream that would generate an infinite list of odd natural numbers. Here's what that looked like:

    odds = Stream.iterate(1, fn x -> x + 2 end)

Now, it's cool that we can build an infinite list so easily, but having a list of odd numbers is only useful if we actually, you know, *use* it for something. So let's do that now, with a little help from Leibniz.

### The Gregory-Leibniz series

Wikipedia states that the Leibniz Formula for π[^1] is

1 - 1/3 + 1/5 - 1/7 + 1/9 - ... = π/4

Because I don't mind improper fractions, let's go ahead and expand this to:

4/1 - 4/3 + 4/5 - 4/7 + 4/9 ... = π

Since this is an infinite series, it turns out our infinite list of odds is just what we need for the denominators in Leibniz's formula. We'll use the Enum module's `reduce` function to generate the summation of a subset of the terms in the series (unless you feel like waiting around for a while... say, the heat death of the universe). First, let's define the function that we pass to `reduce` which handles adding or subtracting 4/n from our running total:

    calc_pi = fn elem, acc ->
      if rem(1 + elem, 4) == 0 do
        acc - (4 / elem)
      else
        acc + (4 / elem)
      end
    end

This function receives the current element in the list of odds (the `elem` parameter) and our current running total (the `acc` parameter), checks if the remainder of (1 + elem) / 4 is 0 (this is how we alternate between adding or subtracting from our running total, as *every other* odd + 1 is divisible by 4), and then adds or subtracts 4 / elem from the running total. The return value of this function is then passed (as the `acc` parameter) to the next iteration.

Now, we'll set up our call to `reduce`. Reduce expects a list of inputs, an initial value, and the reduction function as inputs. We'll get our list of inputs by using `Stream.take`[^2] on our odds stream, set our initial value to 0, and use the `calc_pi` funtion we just defined. Let's do a few iterations and increase our `take` value each time, so we can see the calculation slowly converge towards pi's true value:

    odds |> Stream.take(1) |> Enum.reduce(0, calc_pi)
    4.0

    odds |> Stream.take(10) |> Enum.reduce(0, calc_pi)
    3.0418396189294032

    odds |> Stream.take(100) |> Enum.reduce(0, calc_pi)
    3.1315929035585537

    odds |> Stream.take(1_000) |> Enum.reduce(0, calc_pi)
    3.140592653839794

    odds |> Stream.take(10_000) |> Enum.reduce(0, calc_pi)
    3.1414926535900345

    odds |> Stream.take(100_000) |> Enum.reduce(0, calc_pi)
    3.1415826535897198

    odds |> Stream.take(1_000_000) |> Enum.reduce(0, calc_pi)
    3.1415916535897743

As you can see, the Leibniz formula is not especially quick to converge -- it took 1 million iterations to get to 3.14159 (which is as many digits of pi as I happen to have memorized). Let's try a slightly different infinite series, which should converge a little quicker.

### The Nilakantha series

The Nilakantha series is another infinite series that converges to the true value of pi, with the advantage of being faster than the Leibniz series, but still fairly easy to understand.

3 + 4 / (2 * 3 * 4) - 4 / (4 * 5 * 6) + 4 / (6 * 7 * 8) - 4 / (8 * 9 * 10) + ... = π

We can rewrite our `calc_pi` funtion to use the Nilakantha series fairly painlessly:

    calc_pi = fn elem, acc ->
      denominator = (elem - 1) * elem * (elem + 1)
      if rem(1 + elem, 4) == 0 do
        acc + (4 / denominator)
      else
        acc - (4 / denominator)
      end
    end

While we can still use our `odds` stream to generate the Nilakantha series, if you notice the definition above, we need our stream to start at 3, rather than 1. There are a couple of methods we could use, like adding a check in our `calc_pi` to ignore the iteration where `elem` is 1. We could also skip the first element by calling `Stream.filter` with a filter function like `fn x -> x != 1 end`, which will return true for every number in the stream except for 1. But both of those methods seem kind of... inefficient, given that we'll be doing extra checks for every single element in the list. Let's just redefine `odds` to start at 3, instead of 1:[^3]

    odds = Stream.iterate(3, fn x -> x + 2 end)

Now that we've handwaved away that little hitch, let's see how many iterations it takes to get to a similar level of precision using our new, Nilakantha series-based `calc_pi` function. We'll use `Enum.reduce` again, but start with the accumulator parameter set to 3:

    odds |> Stream.take(1) |> Enum.reduce(3, calc_pi)
    3.1666666666666665

    odds |> Stream.take(10) |> Enum.reduce(3, calc_pi)
    3.1414067184965018

    odds |> Stream.take(100) |> Enum.reduce(3, calc_pi)
    3.1415924109719824

Well... that was certainly a bit quicker! Although it would be nice if we knew exactly what iteration got us to 3.14159. In the next entry, we'll look at how we can do that with another Stream module function.

[^1]: [https://en.wikipedia.org/wiki/Leibniz_formula_for_π](https://en.wikipedia.org/wiki/Leibniz_formula_for_%CF%80)

[^2]: In the previous entry, I used the Enum module's `take` function, but I've switched to the Stream module's implementation here. There's not actually any advantage to doing this here (the Enum.reduce call will force the whole list to load so it can begin processing), but we may see some advantages to Stream's lazy `take` in a future article.

[^3]: Honestly, I'm a little surprised that neither the Stream nor Enum modules contain a `skip` function.